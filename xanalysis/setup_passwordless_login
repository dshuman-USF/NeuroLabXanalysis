#!/usr/bin/expect

set got_password no

set need_setup maybe

log_user 0
if {![file exists ~/.ssh/id_rsa.pub]} {
    spawn -noecho ssh-keygen -q -N {}
    set timeout 100

    expect {
        ": " {send \r}
        eof {}
        timeout close
    }
    wait
    spawn -noecho ssh-add
    expect eof
    wait
    if {![file exists ~/.ssh/id_rsa.pub]} {
        puts "keygen failed"
        exit 1
    }
    set need_setup yes
}

for {set n 0} {$n < $argc} {incr n} {
    set host [lindex $argv $n]

    if {$need_setup == "maybe"} {
        if {![catch {system "ssh -q -o 'BatchMode yes' -o 'ConnectTimeout 1' -o 'StrictHostKeyChecking no' $host true"}]} {
            continue
        }
    }

    if {$got_password == "no"} {
        stty -echo
        send_user "Password for $env(LOGNAME) on $host: "
        set timeout -1
        expect_user -re "(.*)\n"
        set password $expect_out(1,string)
        stty echo
        puts ""
        set got_password "yes"
    }

    set timeout 5
    spawn scp -o {StrictHostKeyChecking no}  $env(HOME)/.ssh/id_rsa.pub $host:./id_rsa.pub
    expect {
        "assword: " {
            send $password\r;
            expect {
		"$ " {close; wait}
		eof {wait}
		timeout {close; wait}
	    }
        }
        eof  {puts "scp died"}
        timeout {close; wait}
    }

    set connected 0
    spawn -noecho ssh -q $host
    expect {
        "assword: " {send $password\r; set connected 1}
        eof  wait
        timeout {close; wait}
    }
    if {$connected} {
        expect "$ "
        send "mkdir -p .ssh; chmod 700 .ssh; cd .ssh; touch authorized_keys; chmod 600 authorized_keys\r"
        expect "$ "
        send "cat ../id_rsa.pub >> authorized_keys; rm ../id_rsa.pub\r"
        expect "$ "
        send exit\r
        close
        wait
    }


    if {[catch {system "ssh -q -o 'BatchMode yes' -o 'ConnectTimeout 1' -o 'StrictHostKeyChecking no' $host true"}]} {
        puts "failed to set up password-less login for $host"
    } else {
#        puts "set up password-less login for $host"
    }
        
}
